path=("${HOME}/bin" /opt/tmux/bin ${path})
fpath=("${HOME}/.zsh/functions" ${fpath})

if [[ "$TERM" = 'xterm' || "$TERM" = 'xterm-256color' ]]; then
  export TERM=xterm-256color-italic
fi

# Set terminal colors.
if whence dircolors >/dev/null; then
  eval "$(dircolors)"
else
  # Absence of dircolors indicates Darwin, use LSCOLORS instead of LS_COLORS
  # for coloring by ls.
  export LSCOLORS='ExFxBxDxCxegedabagacad'

  # Used for setting the zstyle list-colors below.
  # Evaluated by running dircolors on a Linux machine.
  export LS_COLORS='rs=0:di=01;34:ln=01;36:mh=00:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=40;31;01:su=37;41:sg=30;43:ca=30;41:tw=30;42:ow=34;42:st=37;44:ex=01;32:*.tar=01;31:*.tgz=01;31:*.arj=01;31:*.taz=01;31:*.lzh=01;31:*.lzma=01;31:*.tlz=01;31:*.txz=01;31:*.zip=01;31:*.z=01;31:*.Z=01;31:*.dz=01;31:*.gz=01;31:*.lz=01;31:*.xz=01;31:*.bz2=01;31:*.bz=01;31:*.tbz=01;31:*.tbz2=01;31:*.tz=01;31:*.deb=01;31:*.rpm=01;31:*.jar=01;31:*.war=01;31:*.ear=01;31:*.sar=01;31:*.rar=01;31:*.ace=01;31:*.zoo=01;31:*.cpio=01;31:*.7z=01;31:*.rz=01;31:*.jpg=01;35:*.jpeg=01;35:*.gif=01;35:*.bmp=01;35:*.pbm=01;35:*.pgm=01;35:*.ppm=01;35:*.tga=01;35:*.xbm=01;35:*.xpm=01;35:*.tif=01;35:*.tiff=01;35:*.png=01;35:*.svg=01;35:*.svgz=01;35:*.mng=01;35:*.pcx=01;35:*.mov=01;35:*.mpg=01;35:*.mpeg=01;35:*.m2v=01;35:*.mkv=01;35:*.webm=01;35:*.ogm=01;35:*.mp4=01;35:*.m4v=01;35:*.mp4v=01;35:*.vob=01;35:*.qt=01;35:*.nuv=01;35:*.wmv=01;35:*.asf=01;35:*.rm=01;35:*.rmvb=01;35:*.flc=01;35:*.avi=01;35:*.fli=01;35:*.flv=01;35:*.gl=01;35:*.dl=01;35:*.xcf=01;35:*.xwd=01;35:*.yuv=01;35:*.cgm=01;35:*.emf=01;35:*.axv=01;35:*.anx=01;35:*.ogv=01;35:*.ogx=01;35:*.aac=00;36:*.au=00;36:*.flac=00;36:*.mid=00;36:*.midi=00;36:*.mka=00;36:*.mp3=00;36:*.mpc=00;36:*.ogg=00;36:*.ra=00;36:*.wav=00;36:*.axa=00;36:*.oga=00;36:*.spx=00;36:*.xspf=00;36:'
fi

zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt '%SAt %p: Hit TAB for more, or the character to insert%s'
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} m:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'r:|[._-]=** r:|=**'
zstyle ':completion:*' menu select=1
zstyle ':completion:*' select-prompt '%SScrolling active: current selection at %p%s'
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle :compinstall filename "${HOME}/.zshrc"

autoload -Uz compinit
compinit

# Use a colorful prompt.
autoload -U promptinit
promptinit
prompt gentoo

# Setup history.
HISTFILE="${HOME}/.zsh_history"
HISTSIZE=10000
SAVEHIST=10000
setopt appendhistory
setopt extendedhistory
setopt inc_append_history
setopt hist_ignore_all_dups
setopt hist_ignore_space

setopt autocd extendedglob nomatch correctall

# Use emacs style keys.
bindkey -e
bindkey ';3D' emacs-backward-word
bindkey ';3C' emacs-forward-word
bindkey ';5D' emacs-backward-word
bindkey ';5C' emacs-forward-word

# Allow backward-kill-world to delete individual components.
autoload -U select-word-style
select-word-style bash

# Allow editing the command-line.
autoload edit-command-line
zle -N edit-command-line
bindkey '^Xe' edit-command-line

# Set default editor and pager.
export EDITOR=vim
export PAGER=less

# Alias to quickly resource this file.
alias resource="source ${HOME}/.zshrc"

# Set up ls aliases for quicker and easier to read listings.
if ls --color=always >/dev/null 2>&1; then
  # Linux color support.
  alias ls='ls --color=always -Fh'
else
  # Darwin color support.
  alias ls='ls -GFh'
fi
alias l='ls -l'
alias ll='ls -l'
alias la='ls -la'

# Make it quicker to launch tmux
function tmx() {
  if [[ -z "$1" ]]; then
    >&2 echo 'Usage: tmx [NAME]'
    return 1
  fi
  tmux attach-session -t "$1" 2>/dev/null || tmux new-session -s "$1"
}

# Git aliases.
alias git-graph='git log --graph --full-history --first-parent --branches --color --pretty=format:"%x1b[31m%h%x09%x1b[32m%d%x1b[0m%x20%s"'

# Shortcuts for switching between Dvorak and US keyboard layouts.
alias asdf="setxkbmap dvorak"
alias aoeu="setxkbmap us"

# Allow using ~p as an alias for the project directory.
hash -d p="${HOME}/p"

# Source any local .zshrc
if [[ -f "${HOME}/.zshrc_local" ]]; then
  source "${HOME}/.zshrc_local"
fi
